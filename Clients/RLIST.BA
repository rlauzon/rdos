10 REM rlist - Get directory list from the Ron Drive
20 MAXFILES = 2
30 OPEN "com:98N1D" FOR OUTPUT AS #1
40 OPEN "com:98N1D" FOR INPUT AS #2
50 R$="ZZ" +CHR$(0)+CHR$(26)+SPACE$(24)+"F"+CHR$(1)
60 PRINT #1,R$;
70 C$=INPUT$(1,2)
80 IF ASC(C$) <> 17 THEN PRINT "Invalid response code:";ASC(C$):GOTO 190
90 C$=INPUT$(1,2)
100 L=ASC(C$)
110 Q$=INPUT$(L,2)
120 F$=MID$(Q$,1,24)
130 A$=MID$(Q$,25,1)
140 IF ASC(MID$(Q$,1,1)) = 0 THEN GOTO 190
150 PRINT A$; " " ;F$
155 a$=inkey$:if a$="q" or a$="Q" then end
160 R$="ZZ" +CHR$(0)+CHR$(26)+SPACE$(24)+"F"+CHR$(2)
170 PRINT #1,R$;
180 GOTO 70
190 CLOSE #1:CLOSE #2:END

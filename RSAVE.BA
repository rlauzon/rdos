10 REM rload - Save file to Ron Disk
20 MAXFILES = 3
30 INPUT "What file to save on Tandy";L$
40 INPUT "What to name it on disk";F$
45 IF LEN(F$) > 24 THEN PRINT "File name too long":END
50 OPEN "com:98N1D" FOR OUTPUT AS #1
60 OPEN "com:98N1D" FOR INPUT AS #2
80 REM reference file
90 R$="ZZ" +CHR$(0)+CHR$(26)+F$+SPACE$(24-LEN(F$))+"F"+CHR$(0)
100 PRINT #1,R$;:PRINT ".";
110 C$=INPUT$(1,2)
120 IF ASC(C$) <> 17 THEN PRINT "Invalid response code (ref)":GOTO 385
130 C$=INPUT$(1,2)
140 L=ASC(C$)
150 Q$=INPUT$(L,2):Q$=""
159 REM open file for write
160 R$="ZZ" +CHR$(1)+CHR$(1)+CHR$(1)
170 PRINT #1,R$;:PRINT ".";
180 C$=INPUT$(1,2)
190 IF ASC(C$) <> 18 THEN PRINT "Invalid response code (open)":GOTO 385
200 C$=INPUT$(1,2):C$=INPUT$(1,2)
210 IF ASC(C$) <> 0 THEN PRINT "Error opening file:",ASC(C$):GOTO 385
215 OPEN L$ FOR INPUT AS #3
219 REM Read file
220 ON ERROR GOTO 225
221 Q$="":FOR I = 0TO 60:A$=INPUT$(1,3):Q$=Q$+A$:NEXT
225 IF LEN(Q$) = 0 GOTO 330
230 R$="ZZ" +CHR$(4)+CHR$(LEN(Q$))
240 PRINT #1,R$;Q$;:PRINT ".";
250 C$=INPUT$(1,2)
260 C$=INPUT$(1,2):C$=INPUT$(1,2)
270 IF ASC(C$) <> 0 THEN PRINT "Error writing file:",ASC(C$):GOTO 385
310 IF LEN(Q$) < 60 THEN 330 ELSE 220
329 REM close file
330 R$="ZZ" +CHR$(2)+CHR$(0)
340 PRINT #1,R$;:PRINT ".";
350 C$=INPUT$(1,2)
360 IF ASC(C$) <> 18 THEN PRINT "Invalid response code (close)":GOTO 390
370 C$=INPUT$(1,2):C$=INPUT$(1,2)
380 IF ASC(C$) <> 0 THEN PRINT "Error closing file:",ASC(C$)
385 CLOSE #3
390 CLOSE #1:CLOSE #2:END
